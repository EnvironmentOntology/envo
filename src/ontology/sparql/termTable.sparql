PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>  
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>  
PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX oio: <http://www.geneontology.org/formats/oboInOwl#>  
PREFIX obo: <http://purl.obolibrary.org/obo/> 
        
SELECT ?label ?definition (group_concat(distinct ?exact ; separator = "|") AS ?exactSynonym) (group_concat(distinct ?broad ; separator = "|") AS ?broadSynonym) (group_concat(distinct ?narrow ; separator = "|") AS ?narrowSynonym) (group_concat(distinct ?related ; separator = "|") AS ?relatedSynonym) (group_concat(distinct ?subset ; separator = "|") AS ?subsetType) ?uri 
WHERE {
    { ?envo rdfs:label ?l 
     FILTER (regex(str(?envo), "ENVO"))
     BIND (STR(?l)  AS ?label) 
     BIND (STR(?envo)  AS ?uri)
    }
    OPTIONAL{ 
        ?envo obo:IAO_0000115 ?d 
        BIND (STR(?d)  AS ?definition)  
    }
    OPTIONAL{ 
        ?envo oio:hasExactSynonym ?e 
        BIND (STR(?e)  AS ?exact)  
    }
    OPTIONAL { 
        ?envo oio:hasNarrowSynonym ?n 
        BIND (STR(?n)  AS ?narrow)  
    }
    OPTIONAL { 
        ?envo oio:hasBroadSynonym ?b 
        BIND (STR(?b)  AS ?broad)  
    }
     OPTIONAL { 
        ?envo oio:hasRelatedSynonym ?r 
        BIND (STR(?r)  AS ?related)  
    }
    OPTIONAL{ 
        ?envo oio:inSubset ?subset 
    }
    FILTER NOT EXISTS {
        ?envo owl:deprecated ?bool .
    }
    
}
group by ?label ?definition ?uri
order by ASC(?label)
